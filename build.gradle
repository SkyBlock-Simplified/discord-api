plugins {
    id 'java'
    id 'application'
}

project.sourceCompatibility = "17"
project.targetCompatibility = "17"

static def loadEnv() {
    def env = [:]
    try {
        def file = new File('.env')
        def scanner = new Scanner(file)
        while (scanner.hasNextLine()) {
            def line = scanner.nextLine()
            if (line.contains('=')) {
                def pair = line.split('=')
                env.put(pair[0], pair.length == 2 ? pair[1] : '')
            }
        }
    } catch (FileNotFoundException ignored) {
    }
    env.putAll(System.getenv())
    return env
}

def env = loadEnv()

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven {
        name = 'GitHubPackages'
        url = uri('https://maven.pkg.github.com/skyblock-simplified/packages')
        credentials {
            username = env.get('GITHUB_USER')
            password = env.get('GITHUB_PAT')
        }
    }
}

dependencies {
    implementation group: 'com.discord4j', name: 'discord4j-core', version: '3.3.0-SNAPSHOT'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.6'

    // Projects
    implementation project(':api')

    // Annotation Processors
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.26'

    // Test
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'
}

test {
    useJUnitPlatform()
}
